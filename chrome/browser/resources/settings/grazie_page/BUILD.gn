import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash || is_chromeos_lacros || is_linux || is_mac || is_win)

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "grazie_page"
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "grazie_page.html" ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/$preprocess_manifest" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "grazie_page.ts" ]
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  composite = true
  tsconfig_base = "//chrome/browser/resources/settings/tsconfig_base.json"
  in_files = [ "grazie_page.ts" ]
  deps = [
    "//chrome/browser/resources/settings:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
  extra_deps = [ ":preprocess" ]
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/grazie_page_resources.h",
    "grit/grazie_page_resources_map.cc",
    "grit/grazie_page_resources_map.h",
    "grazie_page_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
} 